plugins {
    id 'jacoco'
    id 'com.github.kt3k.coveralls' version '2.8.1'
    id 'com.github.ben-manes.versions' version '0.14.0'
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven'
apply plugin: 'signing'

sourceCompatibility = 1.7
targetCompatibility = 1.7

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
            srcDir file('src/integration-test/java')
        }
        resources.srcDir file('src/integration-test/resources')
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile group: 'com.github.michaelruocco', name: 'http-client', version: '3.0.0'
    compile group: 'com.google.code.gson', name: 'gson', version: '2.8.0'
    compile group: 'joda-time', name: 'joda-time', version: '2.9.7'
    compile group: 'org.apache.commons', name: 'commons-lang3', version: '3.5'
    compile group: 'org.slf4j', name: 'slf4j-api', version: '1.7.24'

    testCompile group: 'junit', name: 'junit', version: '4.12'
    testCompile group: 'org.assertj', name: 'assertj-core', version: '3.6.2'
    testCompile group: 'org.mockito', name: 'mockito-all', version: '1.9.5'
    testCompile group: 'com.github.michaelruocco', name: "wso2-test-containers", version: '1.0.1'
    testCompile group: 'org.slf4j', name: 'slf4j-log4j12', version: '1.7.24'
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath
    outputs.upToDateWhen { false }
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

tasks.withType(Test) {
    systemProperty "javax.net.ssl.trustStore", "$projectDir/truststore/cacerts"
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

gradle.taskGraph.whenReady { graph ->
    if (graph.hasTask(uploadArchives)) {
        signing {
            required true
            sign configurations.archives
        }
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                packaging 'jar'
                name 'WSO2 API Manager Client'
                description 'Library for interacting with the WSO2 API Manager Publisher and Store APIs'
                groupId 'com.github.michaelruocco'
                artifactId 'wso2-api-manager-client'
                version '4.0.0'
                url 'https://github.com/michaelruocco/wso2-api-manager-client'

                developers {
                    developer {
                        id 'mruoc'
                        name 'Michael Ruocco'
                        email 'michael.ruocco@hotmail.com'
                    }
                }

                scm {
                    connection 'scm:git:git@github.com/michaelruocco/wso2-api-manager-client.git'
                    developerConnection 'scm:git:git@github.com/michaelruocco/wso2-api-manager-client.git'
                    url 'git@github.com/michaelruocco/wso2-api-manager-client.git'
                }

                licenses {
                    license {
                        name 'BSD License'
                        url 'http://www.opensource.org/licenses/bsd-license.php'
                        distribution 'repo'
                    }
                }
            }
        }
    }
}